PREFIX part: <http://purl.org/vocab/participation/schema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX connected_to: <http://purl.obolibrary.org/obo/RO_0002170>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX hint: <http://www.bigdata.com/queryHints#>
# specify focus node here
PREFIX node: <http://purl.obolibrary.org/obo/UBERON_0001285> 
SELECT ?s (MIN(?s_label) AS ?slabel) ?p (MIN(?p_label) AS ?plabel) ?o (MIN(?o_label) AS ?olabel)
WHERE {
  {
    SELECT DISTINCT ?s
    WHERE {
      GRAPH <http://reasoner.renci.org/nonredundant> {
        VALUES ?o { node: }
        VALUES ?p_p { connected_to: part_of: }
        ?s ?p ?o .
        ?p rdfs:subPropertyOf* ?p_p .
      }
    }
  }
  UNION
  {
    SELECT DISTINCT ?s
    WHERE {
      GRAPH <http://reasoner.renci.org/nonredundant> {
        VALUES ?o { node: }
        VALUES ?pn_p { connected_to: part_of: }
        ?s ?p1 ?o1 .
        ?o1 ?p2 ?o .
        ?p1 rdfs:subPropertyOf* ?pn_p .
        ?p2 rdfs:subPropertyOf* ?pn_p .
      }
    }
  }
  UNION
  {
    SELECT DISTINCT ?s
    WHERE {
      GRAPH <http://reasoner.renci.org/nonredundant> {
        VALUES ?o { node: }
        VALUES ?pn_p { connected_to: part_of: }
        ?s ?p1 ?o1 .
        ?o1 ?p2 ?o2 .
        ?o2 ?p3 ?o .
        ?p1 rdfs:subPropertyOf* ?pn_p .
        ?p2 rdfs:subPropertyOf* ?pn_p .
        ?p3 rdfs:subPropertyOf* ?pn_p .
      }
    }
  }
  UNION { VALUES ?o { node: } }
  # this hint is required for Blazegraph to run this query efficiently
  hint:Prior hint:runFirst true .
  GRAPH <http://reasoner.renci.org/nonredundant> {
    {
      VALUES ?p_p { connected_to: part_of: }
      ?s ?p ?o .
      ?p rdfs:subPropertyOf* ?p_p .
    }
  }
  ?s rdfs:label ?s_label .
  ?p rdfs:label ?p_label .
  ?o rdfs:label ?o_label .
}
GROUP BY ?s ?p ?o